.needs <std>
import Middleware from "HTTP/Middleware";
import HTTPResponce, HTTPMessage, NotFoundError, NotImplementedError, HTTPMethod from "HTTP";
import {methodToString} from "HTTP" under http;
import Server from "HTTP/Server";
import GET, POST, PUT, DELETE from "HTTP/Endpoints";
import Endpoints from "HTTP/Endpoint";
import Content from "Web/Content";
import string from "String";
import {print} from "String" under str;
import Result from "Utils/Result";
import {accept, reject} from "Utils/Result" under res;
import GetTask from "./src/Content/GetTask";
import PostTask from "./src/Content/PostTask"; 
import DeleteTask from "./src/Content/DeleteTask";
import Index from "./src/Content/UI/Index";
import PlainContent from "./src/Content/UI/PlainContent";
import Map from "Utils/Map";
import Error from "Utils/Error";


class TodoList signs Server {
	Index indexMarkup = new Index();
	PlainContent indexJS = new PlainContent("./js/index.js");
	GetTask getTask = new GetTask();
	PostTask postTask = new PostTask();
	DeleteTask deleteTask = new DeleteTask();

	// HTTPMessage log(HTTPMessage request) : Middleware {
	// 	let method = http.methodToString(request.method);
	// 	str.print(`Request: {method} {request.endpoint}\n`);
	// 	return request;
	// };

	Result home() : GET("/") {
		let responce = new HTTPResponce(my.indexMarkup.render(), "200", "OK");
		responce.addHeader("Content-Type", "text/html");
		return res.accept(responce);
	};

	Result indexJavaScript() : GET("/js/index.js") {
		let responce = new HTTPResponce(my.indexJS.render(), "200", "OK");
		responce.addHeader("Content-Type", "text/javascript");
		return res.accept(responce);
	};

	Map tasks() : Endpoints("/api/tasks") {
		return {
			"GET": [TodoList self, HTTPMessage message] => {
				adr id = message.query.get("id").or(NULL);
				if id != NULL str.print(`ID: ${id}\n`);
				return self.getTask.render(id);
			},
			"POST": [TodoList self, HTTPMessage message] => {
				return self.postTask.render(message.body);
			},
			"DELETE": [TodoList self, HTTPMessage message] => {
			    return message.query.get("id").match({
					"some": [adr taskID, TodoList this] => this.deleteTask.render(taskID),
					"none": [] => res.reject(Error("ID not passed")) 
				}, self);
				// curl command for delete... curl -X DELETE http://localhost:8080/api/tasks?id=1
			}
		};
	};

	TodoList init() {
		return my;
	};
};

int main(){
	let server = new TodoList();
	server.listen();
	return 0;
};
